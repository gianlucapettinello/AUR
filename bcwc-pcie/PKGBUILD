# Maintainer: Maurizio D'Addona <mauritiusdadd@gmail.com>

pkgname="bcwc-pcie-git"
pkgver=r189.6a12079
pkgrel=1
pkgdesc="Reverse engineered Linux driver for the Broadcom 1570 PCIe webcam"
url="https://github.com/patjak/bcwc_pcie"
depends=('kmod')
optdepends=()
makedepends=('linux-headers' 'git')
arch=('i686' 'x86_64')
license=('custom')
install="${pkgname}.install"
_extramodules="extramodules-4.3-ARCH"

# NOTE: AppleCameraInterface can be found in a OS X installation under
#       /System/Library/Extensions/AppleCameraInterface.kext/Contents/MacOS/
source=("${pkgname}::git+https://github.com/patjak/bcwc_pcie.git"
        "AppleCameraInterface")

sha256sums=('SKIP'
            '4667e6828f6bfc690a39cf9d561369a525f44394f48d0a98d750931b2f3f278b')

pkgver()
{
  cd "${srcdir}/${pkgname}"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare()
{
  cd "${srcdir}/${pkgname}/firmware" 

  # patch the extraction script with the hash of your driver

  # This is the default hash
  default_hash="d1db66d71475687a5873dab10a345e2d"

  # This is the hash of the most recet driver
  driver_hash="a9ad5b3dc36025ff92cc41b23b7f2437"

  sed -i -e "s/OSX_HASH=${default_hash}/OSX_HASH=${driver_hash}/g" \
             extract_from_osx.sh

  cp "${srcdir}/AppleCameraInterface" .
  ./extract_from_osx.sh
}

build()
{
  cd "${srcdir}/${pkgname}"
  make ${MAKEFLAGS}

  gzip -9 "facetimehd.ko"
}

package()
{
  cd "${srcdir}/${pkgname}"

  install -dm755 "$pkgdir/usr/lib/modules/$_extramodules/"
  install -m644 'facetimehd.ko.gz' "$pkgdir/usr/lib/modules/$_extramodules/"
 
  install -dm755 "$pkgdir/usr/lib/firmware/facetimehd/"
  install -m644 'firmware/firmware.bin' "$pkgdir/usr/lib/firmware/facetimehd"
}
